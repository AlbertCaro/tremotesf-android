import com.android.build.OutputFile

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlinx-serialization'
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'
    ndkVersion '21.0.6113669'
    defaultConfig {
        applicationId 'org.equeim.tremotesf'
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 32
        versionName '2.0.2'

        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=c++_shared', '-DANDROID_ARM_NEON=true'
            }
        }
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            java.srcDirs 'src/main/kotlin', '../libtremotesf/jni/java'
            jniLibs.srcDirs 'libs'
        }
        google {
            java.srcDirs 'src/google/kotlin'
        }
        fdroid {
            java.srcDirs 'src/fdroid/kotlin'
        }
    }
    externalNativeBuild {
        cmake {
            path '../libtremotesf/CMakeLists.txt'
            version '3.10.2'
        }
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    lintOptions {
        checkReleaseBuilds false
    }
    flavorDimensions('freedom')
    productFlavors {
        google {
            dimension 'freedom'
            buildConfigField 'boolean', 'DONATIONS_GOOGLE', 'true'
        }
        fdroid {
            dimension 'freedom'
            buildConfigField 'boolean', 'DONATIONS_GOOGLE', 'false'
        }
    }
    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'x86', 'arm64-v8a', 'x86_64'
            universalApk true
        }
    }
    applicationVariants.all { variant ->
        def abiCodes = ['armeabi-v7a': 1, 'x86': 2, 'arm64-v8a': 3, 'x86_64': 4]
        variant.outputs.each { output ->
            def baseAbiVersionCode = abiCodes.get(output.getFilter(OutputFile.ABI))
            if (baseAbiVersionCode != null) {
                output.versionCodeOverride = baseAbiVersionCode * 1000 + variant.versionCode
            }
        }
    }
}

def core_version = '1.2.0'
def fragment_version = '1.2.2'
def lifecycle_version = '2.2.0'
def navigation_version = '2.2.1'

dependencies {
    implementation fileTree(include: '*.jar', dir: 'libs')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.14.0'

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.concurrent:concurrent-futures:1.0.0'
    implementation "androidx.core:core:$core_version"
    implementation "androidx.core:core-ktx:$core_version"
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.1.0'
    implementation 'androidx.drawerlayout:drawerlayout:1.0.0'
    implementation "androidx.fragment:fragment:$fragment_version"
    implementation "androidx.fragment:fragment-ktx:$fragment_version"
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    //implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-service:$lifecycle_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'

    implementation 'androidx.work:work-runtime:2.3.3'

    implementation 'com.google.android.material:material:1.1.0'

    implementation 'io.github.raindev:benjamin:1.0.0'
    implementation 'com.simplecityapps:recyclerview-fastscroll:2.0.0'

    googleImplementation 'com.android.billingclient:billing:2.1.0'
}