import com.android.build.OutputFile

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'
    defaultConfig {
        applicationId 'org.equeim.tremotesf'
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 27
        versionName '1.9.3'

        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=c++_shared'
            }
        }
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            java.srcDirs 'src/main/kotlin', '../libtremotesf/jni/java'
            jniLibs.srcDirs 'libs'
        }
    }
    externalNativeBuild {
        cmake {
            path '../libtremotesf/CMakeLists.txt'
            version '3.10.2'
        }
    }
    lintOptions {
        checkReleaseBuilds false
    }
    flavorDimensions("freedom")
    productFlavors {
        google {
            dimension "freedom"
            buildConfigField "boolean", "DONATIONS_GOOGLE", "true"
        }
        fdroid {
            dimension "freedom"
            buildConfigField "boolean", "DONATIONS_GOOGLE", "false"
        }
    }
    splits {
        abi {
            enable true
            reset()
            include "armeabi-v7a", "x86", "arm64-v8a", "x86_64"
            universalApk true
        }
    }
    applicationVariants.all { variant ->
        def abiCodes = ["armeabi-v7a": 1, "x86": 2, "arm64-v8a": 3, "x86_64": 4]
        variant.outputs.each { output ->
            def baseAbiVersionCode = abiCodes.get(output.getFilter(OutputFile.ABI))
            if (baseAbiVersionCode != null) {
                output.versionCodeOverride = baseAbiVersionCode * 1000 + variant.versionCode
            }
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    implementation fileTree(include: '*.jar', dir: 'libs')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.core:core:1.1.0"
    implementation "androidx.core:core-ktx:1.1.0"
    implementation "androidx.coordinatorlayout:coordinatorlayout:1.0.0"
    implementation "androidx.drawerlayout:drawerlayout:1.0.0"
    implementation "androidx.fragment:fragment:1.1.0"
    implementation "androidx.fragment:fragment-ktx:1.1.0"
    implementation "androidx.gridlayout:gridlayout:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "androidx.preference:preference:1.1.0"
    implementation "androidx.viewpager:viewpager:1.0.0"
    implementation 'androidx.work:work-runtime:2.2.0'
    implementation 'androidx.concurrent:concurrent-futures:1.0.0'

    implementation 'com.google.android.material:material:1.0.0'

    implementation 'com.google.code.gson:gson:2.8.6'

    implementation 'org.jetbrains.anko:anko-commons:0.10.8'
    implementation 'org.jetbrains.anko:anko-design:0.10.8'

    implementation 'io.github.raindev:benjamin:1.0.0'
    implementation 'com.simplecityapps:recyclerview-fastscroll:2.0.0'
    implementation 'org.sufficientlysecure:donations:2.6'
}