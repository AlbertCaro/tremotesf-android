import com.android.build.OutputFile

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.2'
    defaultConfig {
        applicationId 'org.equeim.tremotesf'
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 19
        versionName '1.8.1'
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        main.java.srcDirs += '../libtremotesf/jni/java'
        main.jniLibs.srcDirs 'libs'
    }
    lintOptions {
        checkReleaseBuilds false
    }
    flavorDimensions("freedom")
    productFlavors {
        google {
            dimension "freedom"
            buildConfigField "boolean", "DONATIONS_GOOGLE", "true"
        }
        fdroid {
            dimension "freedom"
            buildConfigField "boolean", "DONATIONS_GOOGLE", "false"
        }
    }
    splits {
        abi {
            enable true
            reset()
            include "armeabi-v7a", "x86"
            universalApk true
        }
    }
    applicationVariants.all { variant ->
        def abiCodes = ["armeabi-v7a": 1, "x86": 2]
        variant.outputs.each { output ->
            def baseAbiVersionCode = abiCodes.get(output.getFilter(OutputFile.ABI))
            if (baseAbiVersionCode != null) {
                output.versionCodeOverride = baseAbiVersionCode * 1000 + variant.versionCode
            }
        }
    }
}

repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
}

def androidx_version = '1.0.0-rc02'

dependencies {
    implementation fileTree(include: '*.jar', dir: 'libs')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "androidx.appcompat:appcompat:$androidx_version"
    implementation "androidx.core:core:$androidx_version"
    implementation "androidx.core:core-ktx:$androidx_version"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$androidx_version"
    implementation "androidx.drawerlayout:drawerlayout:$androidx_version"
    implementation "androidx.fragment:fragment:$androidx_version"
    implementation "androidx.fragment:fragment-ktx:$androidx_version"
    implementation "androidx.gridlayout:gridlayout:$androidx_version"
    implementation "androidx.recyclerview:recyclerview:$androidx_version"
    implementation "androidx.preference:preference:$androidx_version"
    implementation "androidx.viewpager:viewpager:$androidx_version"

    implementation 'com.google.android.material:material:1.0.0-rc01'

    implementation 'com.google.code.gson:gson:2.8.5'

    implementation 'org.jetbrains.anko:anko-commons:0.10.5'
    implementation 'org.jetbrains.anko:anko-design:0.10.5'

    implementation 'com.github.amjjd:java-alphanum:0.2'
    implementation 'io.github.raindev:benjamin:1.0.0'
    implementation 'com.simplecityapps:recyclerview-fastscroll:1.0.18'
    implementation 'org.sufficientlysecure:donations:2.6'
}